#!/usr/bin/perl
use strict;
use warnings;
use Getopt::Std;

our ($opt_r,$opt_O,$opt_n);

my $dryrun=0;
my $MAX_RECURSIONS=6;

sub submit_one {
	my ($script,$options,$recursion_level)=@_;
	if ($recursion_level > $MAX_RECURSIONS) {
		print "too many recursions: $recursion_level > $MAX_RECURSIONS\n";
		return 4223;
	}
	if (-s $script) {
		my $jobidfile="$script.submitted";
		if (! -s $jobidfile) {
			$script=~/^(.*\/)([^\/]+)$/;
			my ($dir,$script_base)=($1,$2);
			chdir $dir;

			my $dep_option='';
			my $dep_glob_file="$script_base.depends";
			if (-s $dep_glob_file) {
				my $dep_glob_fh;
				my @dep_files;
				my @dep_ids;
				open($dep_glob_fh,$dep_glob_file);
				while (<$dep_glob_fh>) {
					chomp;
					s/#.*//;
					next if (/^\s*$/);
					push @dep_files,glob;
				}
				close($dep_glob_fh);
				if (@dep_files < 1) {
					print "not submitting: $script_base.depends exists, but no matching files found";
					return 4242;
				}
				foreach my $dep (@dep_files) {
					next if (-f "$dep.completed");
					my $dep_id;
					if (! -s "$dep.submitted") {
						my $retval=submit_one($dep,$options,$recursion_level+1);
						$dep_id=(1000000-$recursion_level) if ($dryrun and $retval==0);
					}
					if ((! defined $dep_id) and (-s "$dep.submitted")) {
						my $dep_fh;
						open($dep_fh,"$dep.submitted");
						$_=<$dep_fh>;
						$dep_id=$_ unless (/^\s*$/);
						chomp;
						close($dep_fh);
					}
					push @dep_ids,$dep_id if (defined $dep_id);
				}
				if (@dep_ids) {
					$dep_option='-hold_jid ' . join(',',@dep_ids);
				}
			}

			my $qsub_cmd="qsub $options $dep_option $script_base";
			if ($dryrun) {
				print STDERR "dryrun: would run '$qsub_cmd'\n";
				return(0);
			}

			chomp (my $resultstring=`$qsub_cmd`);
			my $qsubret=$?;

			if ($resultstring=~/^Your job ([0-9]+) .*/) {
				open(my $fh,">$jobidfile");
				print $fh "$1\n";
				close($fh);
				print "Job $1 submitted\n";
				return($qsubret);
			} else {
				print "Problem submitting job: $resultstring\n";
				return(4200);
			}
		} else {
			print "$script already submitted\n";
			return(0);
		}
	} else {
		print "Cannot run $script: no such file or directory!\n";
		return(4201);
	}
}

getopts("rO:n");
die "No scriptname" if (@ARGV < 1);
$dryrun=1 if (defined $opt_n);

chomp(my $cwd=`pwd`);

my @scripts;
if ($opt_r) {
	foreach my $tmpl (@ARGV) {
		if ($tmpl =~ /\//) {
			die "No '/' in filenames when recursing, please!\n";
		} else {
			open(my $find,"find $cwd -type f -name $tmpl|");
			push @scripts,map { chomp; $_ } <$find>;
			close($find);
		}
	}
} else {
	@scripts=map { "$cwd/$_" } @ARGV;
}
my $qsub_global_opts = (defined $opt_O ? $opt_O : '');

foreach my $script (@scripts) {
	print "script: $script\n";
	submit_one($script,"$qsub_global_opts",0);
}

